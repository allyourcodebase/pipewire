//! Generates the pipewire client config. We can almost use `addConfigHeader` for this with the
//! `autoconf_at` style, but not quite as it adds a c style comment to the first line (explaining
//! that the file is generated) which isn't allowed by this syntax.

const std = @import("std");
const options = @import("options");
const assert = std.debug.assert;

pub fn main() !void {
    var gpa = std.heap.GeneralPurposeAllocator(.{ .thread_safe = false }){};
    defer if (gpa.deinit() != .ok) @panic("leak detected");
    const allocator = gpa.allocator();

    const args = try std.process.argsAlloc(allocator);
    defer std.process.argsFree(allocator, args);

    const input_path = args[1];
    const output_path = args[2];
    assert(args.len == 3);

    const cwd = std.fs.cwd();

    const input = try cwd.openFile(input_path, .{});
    defer input.close();

    var input_buf: [4096]u8 = undefined;
    var reader = input.readerStreaming(&input_buf);

    const output = try cwd.createFile(output_path, .{});
    defer output.close();

    var output_buf: [4096]u8 = undefined;
    var writer = output.writerStreaming(&output_buf);

    // Ideally we'd just use embed file rather than emit this warning but I'm having trouble
    // getting `addEmbedPath` to work, so this will do for now. Not sure if this is a bug or user
    // error.
    try writer.interface.writeAll("# ");
    try writer.interface.splatByteAll('#', 80 - 2);
    try writer.interface.writeAll("\n");
    try writer.interface.writeAll("# Generated by Zig's build system. Do not edit.\n");
    try writer.interface.writeAll("# ");
    try writer.interface.splatByteAll('#', 80 - 2);
    try writer.interface.writeAll("\n\n");

    while (true) {
        _ = reader.interface.streamDelimiter(&writer.interface, '@') catch |err| switch (err) {
            error.EndOfStream => break,
            else => return err,
        };
        reader.interface.toss(1);
        const name = try reader.interface.takeSentinel('@');
        inline for (@typeInfo(options).@"struct".decls) |decl| {
            if (std.mem.eql(u8, name, decl.name)) {
                try writer.interface.writeAll(@field(options, decl.name));
                break;
            }
        } else std.debug.panic("missing option {s}", .{name});
    }

    try writer.interface.flush();
    try output.sync();
}
